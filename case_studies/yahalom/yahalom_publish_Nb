principals: Alice, Bob, Server
unary-equals: implicit

// initial assumptions
Alice [
  knows private Kas
]

Bob [
  knows private Kbs
]

Server [
  knows private Kas, Kbs
]

// session
Alice [
  generates Na
]
Alice -> Bob: Alice, Na

Bob [
  generates Nb
  m1 = ENC(Kbs, {Alice, Na, Nb})
]
Bob -> Server: Bob, m1, Nb // <- Bob publishes Nb here; in the original protocol he doesn't

Server [
  {Alice, Na, Nb} = DEC(Kbs, m1)
  generates Kab
  ma = ENC(Kas, {Kab, Na, Nb})
  mb = ENC(Kbs, {Alice, Kab})
]
Server -> Alice: ma, mb

Alice [
  {Kab, Na, Nb} = DEC(Kas, ma)
  auth = ENC(Kab, Nb)
]
Alice -> Bob: mb, auth

Bob [
  {Alice, Kab} = DEC(Kbs, mb)
  Nb = DEC(Kab, auth)
]

queries [
  // warning: the second property requires about 15GB RAM
  authentication? Alice->Bob: auth
  confidentiality? Bob's Kab
  
  // The rest should suceed, but none terminate.
  // We made sure that the protocol is executable by finding the trace manually.
  // The heuritics push the correct options quite low, so it's unsurprising that it's not found automatically.
  // executable?
  // confidentiality? Alice's Kab
  // confidentiality? Server's Kab
]