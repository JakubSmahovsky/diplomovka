principals: Alice, Bob, Server
unary-equals: implicit

// initial assumptions
Alice [
  knows private Kas
  knows public Bob
]

Bob [
  knows private Kbs
]

Server [
  knows private Kas, Kbs
]

// session
Alice [
  generates N, Na
  m1 = ENC(Kas, {Na, N, Alice, Bob})
]
Alice -> Bob: N, Alice, Bob, m1

Bob [
  generates Nb
  m2 = ENC(Kbs, {Nb, N, Alice, Bob})
]
Bob -> Server: N, Alice, Bob, m1, m2

Server [
  {Na, N, Alice, Bob} = DEC(Kas, m1)
  {Nb, N, Alice, Bob} = DEC(Kbs, m2)
  
  generates Kab
  m3 = ENC(Kbs, {Nb, Kab})
  m4 = ENC(Kas, {Na, Kab})
]
Server -> Bob: N, m3, m4

Bob [
  {Nb, Kab} = DEC(Kbs, m3)
]
Bob -> Alice: N, m4

Alice [
  {Na, Kab} = DEC(Kas, m4)
]

queries [
  executable?
  confidentiality? Alice's Kab
  confidentiality? Bob's Kab
  
  // no attack but doesn't terminate
  confidentiality? Server's Kab
]