// The Needham Schroeder Symmetric Key protocol
// The attack requires compromising Kab after the protocol finishes.
// We can simulate this by having one of the principals send it to themselves.
// The attack is based on the fact that messages from one session
// can be replayed to Bob in another session.
// The adversary makes Bob accept an old compromised key Kab.

principals: Alice, Bob, Server
unary-equals: implicit

Alice [
  knows public Bob
  knows private Kas
]

Bob [
  knows public Alice
  knows private Kbs
]

Server [
  knows public Alice, Bob
  knows private Kas, Kbs
]

Alice [
  generates Na
]
Alice -> Server: Alice, Bob, Na

Server [
  generates Kab
  ma = ENC(Kas, {Na, Bob, Kab, ENC(Kbs, {Kab, Alice})})
]
Server -> Alice: ma

Alice [
  {Na, Bob, Kab, mb} = DEC(Kas, ma)
]
Alice -> Bob: mb

Bob [
  {Kab, Alice} = DEC(Kbs, mb)
  generates Nb
  challenge = ENC(Kab, Nb)
]
Bob -> Alice: challenge

Alice [
  Nb = DEC(Kab, challenge)
  response = ENC(Kab, {Nb, '1'})
]
Alice -> Bob: response

Bob [
  {Nb, '1'} = DEC(Kab, response)
]

Bob -> Bob: Kab

queries [
  injective-authentication? Alice -> Bob: response
]